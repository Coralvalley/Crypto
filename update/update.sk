# When you update the plugin, this file gets downloaded and almost immediately deleted.
# First the main plugin gets disabled and then there is a short 2 second wait.
# After that the disabled main plugin file gets deleted and immediately replaced by the new version
# Then when the new file is downloaded it will be reloaded.
# The deletion of this file happens in the main plugin file.

# Note: The deletion of the main plugin file doesn't result into your playerdata files being deleted!


on load:
    # Disable the main plugin file 'crypto.sk'
    send "Disabling main script" to console
    if file path "plugins/Skript/scripts/crypto/-crypto.sk" is missing:
        disable script "crypto/crypto"
        if file path "plugins/Skript/scripts/crypto/crypto.sk" is missing:
            send "&aSuccesfully disabled main script" to console
        else:
            send "&cUnable to disable main script" to console
    else:
        send "&aMain script is already disabled" to console
    wait 2 seconds

    # Disable the website file 'website.sk'
    send "Disabling website script" to console
    if file path "plugins/Skript/scripts/crypto/-website.sk" is missing:
        disable script "crypto/website"
        if file path "plugins/Skript/scripts/crypto/website.sk" is missing:
            send "&aSuccesfully disabled website script" to console
        else:
            send "&cUnable to disable website script" to console
    else:
        send "&aWebsite script is already disabled" to console
    wait 2 seconds

    # Delete the main plugin file now called '-crypto.sk'
    send "Deleting main script" to console
    if file path "plugins/Skript/scripts/crypto/-crypto.sk" is not missing:
        delete file "plugins/Skript/scripts/crypto/-crypto.sk"
    if file path "plugins/Skript/scripts/crypto/-crypto.sk" is missing:
        send "&aSuccesfully deleted main script" to console
    else:
        send "&cUnable to delete main script" to console
    wait 2 seconds

    # Delete the website script now called '-website.sk'
    send "Deleting website script" to console
    delete file "plugins/Skript/scripts/crypto/-website.sk"
    if file path "plugins/Skript/scripts/crypto/-website.sk" is missing:
        send "&aSuccesfully deleted website script" to console
    else:
        send "&cUnable to delete webste script" to console
    wait 2 seconds

    # Download the new main plugin file called 'crypto.sk'
    send "Downloading main script" to console
    download file from "https://raw.githubusercontent.com/Coralvalley/Crypto/main/crypto.sk" to file "plugins/Skript/scripts/crypto/crypto.sk"
    if file path "plugins/Skript/scripts/crypto/crypto.sk" is not missing:
        send "&aSuccesfully downloaded main script" to console
    else:
        send "&cUnable to download main script is website online?" to console
    wait 2 seconds

    # Download the new website script file called 'website.sk'
    send "Downloading website script" to console
    download file from "https://raw.githubusercontent.com/Coralvalley/Crypto/main/website.sk" to file "plugins/Skript/scripts/crypto/website.sk"
    if file path "plugins/Skript/scripts/crypto/website.sk" is not missing:
        send "&aSuccesfully downloaded website script" to console
    else:
        send "&cUnable to download website script is website online?" to console
    wait 2 seconds

    # Enable and reload the main plugin file 'crypto.sk'
    reload script "crypto/crypto"

    # Enable and reload the website script 'website.sk'
    reload script "crypto/website"

    # Set the version of the plugin in config.yml to the new version
    set {_version} to text from "https://raw.githubusercontent.com/Coralvalley/Crypto/main/version.yml"
    set yaml value "DoNotChange.version" in "crypto-config" to {_version}
    save yaml "crypto-config"